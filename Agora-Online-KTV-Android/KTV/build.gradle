if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("firebase")) {
    apply plugin: 'com.google.gms.google-services'
}

Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
properties.load(inputStream)

android {
    compileSdkVersion COMPILESDKVERSION
    buildToolsVersion BUILDTOOLSVERSION

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "io.agora.ktv"
        }
        minSdkVersion MINSDKVERSION
        targetSdkVersion TARGETSDKVERSION
        versionCode 9
        versionName "4.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true

        buildConfigField("String", "APP_ID", "\"${properties.getProperty("APP_ID", "")}\"")
        buildConfigField("String", "RTC_TOKEN", "\"${properties.getProperty("RTC_TOKEN", "")}\"")
        buildConfigField("String", "APP_CERTIFICATE", "\"${properties.getProperty("APP_CERTIFICATE", "")}\"")
    }

    resourcePrefix "ktv_"

    signingConfigs {
        release {
            keyAlias "key0"
            keyPassword "123456"
            storeFile file('./keystore/testkey.jks')
            storePassword "123456"
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding = true
    }

    flavorDimensions "test"

    if (isModule.toBoolean()) {
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "KTV-${versionName}-${variant.buildType.name}-local-${releaseTime()}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //implementation files('libs/agora-rtc-sdk.jar')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //implementation 'com.github.AgoraIO-Community:LrcView-Android:1.0.5'

    implementation project(':data')
    implementation project(':AgoraDynamicKey')
    implementation project(':lrcview')
    //implementation 'com.github.AgoraIO-Community:LyricsView:1.0.13'
    //implementation 'io.agora.rtc:agora-special-full:4.2.3.1'

}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("UTC"))
}