if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("firebase")) {
    apply plugin: 'com.google.gms.google-services'
}

Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
properties.load(inputStream)

android {
    compileSdkVersion COMPILESDKVERSION
    buildToolsVersion BUILDTOOLSVERSION

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "io.agora.ktv"
        }
        minSdkVersion MINSDKVERSION
        targetSdkVersion TARGETSDKVERSION
        versionCode 9
        versionName "2.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true

        buildConfigField("String", "RTC_APP_ID", "\"${properties.getProperty("RTC_APP_ID", "")}\"")
        buildConfigField("String", "RTC_TOKEN", "\"${properties.getProperty("RTC_TOKEN", "")}\"")
        buildConfigField("String", "MCC_APP_ID", "\"${properties.getProperty("MCC_APP_ID", "")}\"")
        buildConfigField("long", "MCC_UID", "${properties.getProperty("MCC_UID", "-1")}")
        buildConfigField("String", "MCC_RTM_TOKEN", "\"${properties.getProperty("MCC_RTM_TOKEN", "")}\"")
    }

    resourcePrefix "ktv_"

    signingConfigs {
        release {
            keyAlias "agora"
            keyPassword "123456"
            storeFile file('./keystore/agora.jks')
            storePassword "123456"
        }
        debug {
            keyAlias "agora"
            keyPassword "123456"
            storeFile file('./keystore/agora.jks')
            storePassword "123456"
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding = true
    }

    flavorDimensions "test"

    if (isModule.toBoolean()) {
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "KTV-${versionName}-${variant.buildType.name}-local.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation files('libs/agora-rtc-sdk.jar')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //Agora SDK
    //implementation 'io.agora.rtc:full-sdk:3.5.1'

    implementation project(':data')
    implementation project(':lrcview')
}